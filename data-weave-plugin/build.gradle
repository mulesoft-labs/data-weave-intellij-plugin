sourceSets {
    main {
        java {
            srcDir 'src/main/gen'
        }
    }
}

configurations {
    agentServerLibs
}

dependencies {
    implementation(group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.14.0')
    implementation(group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.14.0')
    implementation(group: 'org.mule.weave', name: 'debugger', version: weaveVersion)
    implementation group: 'org.mule.weave', name: 'agent-client', version: weaveAgentVersion
    implementation(group: 'org.mule.weave', name: 'parser', version: weaveVersion)
    implementation(group: 'org.mule.weave', name: 'migrant', version: migrantVersion)
    implementation(group: 'org.mule.als', name: 'als-server_2.12', version: alsVersion)
    implementation(group: 'com.mulesoft.connectivity', name: 'rest-sdk-aml', version: restSdkVersion)
    implementation(group: 'com.mulesoft.connectivity', name: 'rest-sdk-commons', version: restSdkVersion)
    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.12.2'


    agentServerLibs group: 'org.mule.weave', name: 'agent-server', version: weaveAgentVersion, {
        exclude group: 'org.scala-lang', module: 'scala-library'
    }
}

intellij {
    pluginName = 'data-weave-plugin-v2'
    plugins = ['maven', 'properties', 'java', 'yaml']
    updateSinceUntilBuild = false
}

task downloadAgentServerDependencies(type: Copy) {
    from configurations.agentServerLibs
    into "$projectDir/src/main/resources/agent-server-libs"
    include "*.jar"
    // Rename as zip file because shadow plugin
    // see: https://imperceptiblethoughts.com/shadow/configuration/dependencies/#embedding-jar-files-inside-your-shadow-jar
    rename("(.*)-([0-9]\\d*)\\.(\\d+)\\.(\\d+)(.*)", '$1.jar')
}

task cleanAgentServerDependencies(type: Delete) {
    delete "$projectDir/src/main/resources/agent-server-libs"
}

downloadAgentServerDependencies.dependsOn(cleanAgentServerDependencies)
processResources.dependsOn(downloadAgentServerDependencies)